buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "http://dl.bintray.com/ihmcrobotics/maven-release"
        }
    }
    dependencies {
        classpath "gradle.plugin.us.ihmc.gradle:ihmc-build:0.0.19"
        classpath "gradle.plugin.us.ihmc.continuousIntegration:IHMCContinuousIntegrationGradlePlugin:0.10.8"
    }
}

apply plugin: "us.ihmc.gradle.ihmc-build"
apply plugin: "us.ihmc.continuousIntegration.ihmc-continuous-integration-plugin"

if (!rootProject.name.equals('ihmc-open-robotics-software'))
{
    evaluationDependsOn(':ihmc-open-robotics-software')
}

sourceCompatibility = 1.8
version = "${getOpenRoboticsSoftwareProject().version}"
project.ext.fullVersion = "${getOpenRoboticsSoftwareProject().ext.fullVersion}"
project.ext.vcsUrl = "${getOpenRoboticsSoftwareProject().ext.vcsUrl}"
project.ext.licenseURL = "http://www.apache.org/licenses/LICENSE-2.0.txt"
project.ext.licenseName = "Apache License, Version 2.0"
project.ext.bintrayLicenseName = "Apache-2.0"

repositories ihmc.ihmcDefaultArtifactProxies()

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven {
        url "http://artifactory.ihmc.us/artifactory/thirdparty/"
    }
    maven {
        url "http://artifactory.ihmc.us/artifactory/releases/"
    }
}

def strings = ['ihmc', 'robotics']
ihmc.configureForIHMCOpenSourceBintrayPublish(false, "mavenJava", "maven-release", strings)

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.7'

    compile group: 'us.ihmc', name: 'euclid-core', version: '0.4.3'
    compile group: 'org.georegression', name: 'georegression', version: '0.11'
    compile group: 'org.ddogleg', name: 'ddogleg', version: '0.7'
    compile group: 'org.apache.xmlgraphics', name: 'batik-dom', version: '1.7'
    compile group: 'org.apache.ant', name: 'ant', version: '1.9.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    compile group: 'com.martiansoftware', name: 'jsap', version: '2.1'
    compile group: 'org.ros.rosjava_core', name: 'rosjava', version: '0.2.1'
    compile group: 'org.ejml', name: 'core', version: '0.30'
    compile group: 'org.ejml', name: 'dense64', version: '0.30'
    compile group: 'org.boofcv', name: 'io', version: '0.24.1'
    compile group: 'org.boofcv', name: 'ip', version: '0.24.1'
    compile group: 'org.boofcv', name: 'geo', version: '0.24.1'
    compile group: 'org.boofcv', name: 'calibration', version: '0.24.1'
    compile group: 'org.boofcv', name: 'visualize', version: '0.24.1'
    compile group: 'org.ros.rosjava_bootstrap', name: 'message_generation', version: '0.2.1'
    compile group: 'com.esotericsoftware.kryo', name: 'kryo', version: '2.20'
    compile group: 'us.ihmc.thirdparty.jme', name: 'jme3-core', version: '3.1.0-internal4'
    compile group: 'com.github.wendykierp', name: 'JTransforms', version: '3.1'
    compile group: 'org.ros.rosjava_messages', name: 'sensor_msgs', version: '1.11.7'
    compile group: 'org.ros.rosjava_messages', name: 'multisense_ros', version: '3.4.2'
    compile group: 'us.ihmc.thirdparty.org.opencv', name: 'opencv', version: '3.2.0-170503'
    compile 'org.ros.rosjava_messages:geometry_msgs:1.11.9'
    compile "org.boofcv:recognition:0.24.1"

    testCompile group: 'us.ihmc', name: 'ihmc-continuous-integration-framework', version: '0.9.4'

    compile ihmc.getProjectDependency(":ihmc-humanoid-behaviors")
    compile ihmc.getProjectDependency(":Vicon")
    compile ihmc.getProjectDependency(":common-walking-control-modules")
    compile ihmc.getProjectDependency(":ihmc-avatar-interfaces")
    compile ihmc.getProjectDependency(":ihmc-jmonkey-engine-toolkit")
    compile ihmc.getProjectDependency(":ihmc-humanoid-robotics")
    compile ihmc.getProjectDependency(":ihmc-communication")
    compile ihmc.getProjectDependency(":ihmc-java-toolkit")
    compile ihmc.getProjectDependency(":ihmc-java-extensions")
    compile group: 'us.ihmc', name: 'ihmc-commons', version: '0.11.0-alpha'
    compile ihmc.getProjectDependency(":ihmc-perception")
    compile ihmc.getProjectDependency(":ihmc-robotics-toolkit")
    compile ihmc.getProjectDependency(":ihmc-ros-tools")
    compile ihmc.getProjectDependency(":ihmc-whole-body-controller")
    compile ihmc.getProjectDependency(":ihmc-robot-data-logger")
    compile ihmc.getProjectDependency(":robotiq-hand-drivers")
    compile ihmc.getProjectDependency(":model-file-loader")
    compile ihmc.getProjectDependency(":sensor-processing")
    compile ihmc.getProjectDependency(":SimulationConstructionSet")
    compile ihmc.getProjectDependency(":SimulationConstructionSetTools")
    compile ihmc.getProjectDependency(":IHMCGraphicsDescription")
    compile ihmc.getProjectDependency(":ihmc-robot-description")
    compile ihmc.getProjectDependency(":ihmc-robot-models")
    compile ihmc.getProjectDependency(":ihmc-simulation-toolkit")
    compile ihmc.getProjectDependency(":ihmc-robot-data-visualizer")
    compile ihmc.getProjectDependency(":ihmc-manipulation-planning")

    testCompile ihmc.getProjectTestDependency(":ihmc-robotics-toolkit")
    testCompile ihmc.getProjectTestDependency(":ihmc-java-extensions")
    testCompile group: 'us.ihmc', name: 'ihmc-commons-testing', version: '0.11.0-alpha'
    testCompile ihmc.getProjectTestDependency(":common-walking-control-modules")
    testCompile ihmc.getProjectTestDependency(":ihmc-avatar-interfaces")
    testCompile ihmc.getProjectTestDependency(":ihmc-humanoid-robotics")
    testCompile ihmc.getProjectTestDependency(":sensor-processing")
    testCompile ihmc.getProjectTestDependency(":ihmc-simulation-toolkit")
}

apply plugin: 'application'

applicationName = "IHMCAtlasAPI"
mainClassName = "us.ihmc.atlas.AtlasROSAPINetworkProcessor"
applicationDefaultJvmArgs = ["-Xmx4g"]

jar {
    manifest {
        attributes(
                "Created-By": "IHMC Gradle Build Script",
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor": "IHMC",

                "Bundle-Name": project.name,
                "Bundle-Version": project.version,
                "Bundle-License": "${project.ext.licenseURL}",
                "Bundle-Vendor": "IHMC")
    }
}

startScripts {
    outputs.upToDateWhen { false }

    doLast {
        def simUnixScriptDemo01 = file(outputDir.canonicalPath + "/${applicationName}SimulatorDemo01")
        simUnixScriptDemo01.text = unixScript.text.replace("IHMCAtlasAPI", "IHMCAtlasAPISimulator").replace("AtlasROSAPINetworkProcessor", "AtlasDemo01ROSAPISimulator")
        simUnixScriptDemo01.setExecutable(true)

        def simWindowsScriptDemo01 = file(outputDir.canonicalPath + "/${applicationName}SimulatorDemo01.bat")
        simWindowsScriptDemo01.text = windowsScript.text.replace("IHMCAtlasAPI", "IHMCAtlasAPISimulator").replace("AtlasROSAPINetworkProcessor", "AtlasDemo01ROSAPISimulator")

        def simUnixScriptFinals = file(outputDir.canonicalPath + "/${applicationName}SimulatorFinals")
        simUnixScriptFinals.text = unixScript.text.replace("IHMCAtlasAPI", "IHMCAtlasAPISimulator").replace("AtlasROSAPINetworkProcessor", "AtlasFinalsROSAPISimulator")
        simUnixScriptFinals.setExecutable(true)

        def simWindowsScriptFinals = file(outputDir.canonicalPath + "/${applicationName}SimulatorFinals.bat")
        simWindowsScriptFinals.text = windowsScript.text.replace("IHMCAtlasAPI", "IHMCAtlasAPISimulator").replace("AtlasROSAPINetworkProcessor", "AtlasFinalsROSAPISimulator")

        def simUnixScriptVisualizer = file(outputDir.canonicalPath + "/IHMCRemoteAtlasVisualizer")
        simUnixScriptVisualizer.text = unixScript.text.replace("IHMCAtlasAPI", "IHMCRemoteAtlasVisualizer").replace("AtlasROSAPINetworkProcessor", "RemoteAtlasVisualizer")
        simUnixScriptVisualizer.setExecutable(true)

        def simWindowsScriptVisualizer = file(outputDir.canonicalPath + "/IHMCRemoteAtlasVisualizer.bat")
        simWindowsScriptVisualizer.text = windowsScript.text.replace("IHMCAtlasAPI", "IHMCRemoteAtlasVisualizer").replace("AtlasROSAPINetworkProcessor", "RemoteAtlasVisualizer")

        def simUnixScriptLogger = file(outputDir.canonicalPath + "/IHMCLogger")
        simUnixScriptLogger.text = unixScript.text.replace("IHMCAtlasAPI", "IHMCLogger").replace("us.ihmc.atlas.AtlasROSAPINetworkProcessor", "us.ihmc.robotDataLogger.logger.YoVariableLoggerDispatcher")
        simUnixScriptLogger.setExecutable(true)

        def simWindowsScriptLogger = file(outputDir.canonicalPath + "/IHMCLogger.bat")
        simWindowsScriptLogger.text = windowsScript.text.replace("IHMCAtlasAPI", "IHMCLogger").replace("us.ihmc.atlas.AtlasROSAPINetworkProcessor", "us.ihmc.robotDataLogger.logger.YoVariableLoggerDispatcher")

        def simUnixScriptGazebo = file(outputDir.canonicalPath + "/IHMCAtlasGazeboController")
        simUnixScriptGazebo.text = unixScript.text.replace("IHMCAtlasAPI", "IHMCAtlasGazeboController").replace("us.ihmc.atlas.AtlasROSAPINetworkProcessor", "us.ihmc.atlas.gazebo.AtlasGazeboControllerFactory")
        simUnixScriptGazebo.setExecutable(true)

    }
}

applicationDistribution.from(startScripts.outputDir) {
    into "bin"
}
